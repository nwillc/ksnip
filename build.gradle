buildscript {
    repositories {
        jcenter()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version",
                "gradle.plugin.com.github.nwillc:vplugin:$vplugin_version"
    }
}

group = 'com.github.nwillc'
version = '1.0-SNAPSHOT'


apply plugin: 'kotlin'
apply plugin: 'com.github.nwillc.vplugin'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",
            "no.tornado:tornadofx:$tornadofx_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"

    testCompile "org.junit.jupiter:junit-jupiter-engine:$jupiter_version",
            "org.junit.platform:junit-platform-runner:$junit_platform_version",
            "org.assertj:assertj-core:$assertj_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = jvm_version
}

compileTestKotlin {
    kotlinOptions.jvmTarget = jvm_version
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.nwillc.ksnip.app.SnippetsApp'
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

test {
    useJUnitPlatform()
}

task osxApp {
    dependsOn jar
    
    doLast {
        def dist = "${buildDir}/app/${project.name}.app"
        mkdir("$dist/Contents/MacOS")
        mkdir("$dist/Contents/Resources")
        ant.copy(file: "src/main/app/Info.plist", todir: "$dist/Contents")
        ant.copy(file: "src/main/app/launcher", todir: "$dist/Contents/MacOS") {
            filterset {
                filter(token: 'APP_JAR', value: "${project.name}-${version}.jar")
                filter(token: 'APP_NAME', value: project.name)
                filter(token: 'JAVA_VERSION', value: jvm_version)
            }
        }
        ant.copy(file: "src/main/app/application.icns", todir: "$dist/Contents/Resources")
        ant.copy(file: "build/libs/${project.name}-${version}.jar", todir: "$dist/Contents/MacOS")
        project.exec {
            commandLine('chmod', '+x', "$dist/Contents/MacOS/launcher")
        }
    }
}


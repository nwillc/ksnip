buildscript {
    repositories {
        jcenter()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version",
                "gradle.plugin.com.github.nwillc:vplugin:$vplugin_version",
                "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_plugin_version"
    }
}

group = 'com.github.nwillc'
version = '1.1.1'


apply plugin: 'kotlin'
apply plugin: 'com.github.nwillc.vplugin'
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",
            "no.tornado:tornadofx:$tornadofx_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"

    testCompile "org.junit.jupiter:junit-jupiter-engine:$jupiter_version",
            "org.junit.platform:junit-platform-runner:$junit_platform_version",
            "org.assertj:assertj-core:$assertj_version"
    
    testCompile ("org.jetbrains.spek:spek-api:$spek_engine_version") {
                exclude group: 'org.jetbrains.kotlin'
            }

    testRuntime ("org.jetbrains.spek:spek-junit-platform-engine:$spek_engine_version") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = jvm_version
}

compileTestKotlin {
    kotlinOptions.jvmTarget = jvm_version
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.nwillc.ksnip.SnippetsApp'
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

test {
    useJUnitPlatform()
}

task osxApp {
    dependsOn jar
    
    doLast {
        def dist = "${buildDir}/app/${project.name}.app"

        mkdir("$dist/Contents/MacOS")
        mkdir("$dist/Contents/Java")
        mkdir("$dist/Contents/Resources")

        ant.copy(file: "src/main/app/Info.plist", todir: "${dist}/Contents") {
            filterset {
                filter(token: 'APP_NAME', value: project.name)
                filter(token: 'VERSION', value: version)
                filter(token: 'APP_MAIN', value: 'com.github.nwillc.ksnip.SnippetsApp')
            }
        }
        ant.copy(file: "src/main/app/JavaAppLauncher", todir: "${dist}/Contents/MacOS")
        ant.copy(file: "build/libs/${project.name}-${version}.jar", todir: "${dist}/Contents/Java")
        ant.copy(file: "src/main/app/application.icns", todir: "${dist}/Contents/Resources")
        ant.copy(todir: "${dist}/Contents/Resources/en.lproj") {
            fileset(dir: "src/main/app/en.lproj")
        }

        project.exec {
            commandLine('chmod', '+x', "$dist/Contents/MacOS/JavaAppLauncher")
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "${buildDir}/javadoc"
}

